plugins {
	id "java"
	id "maven-publish"
	id "com.github.johnrengelman.shadow" version "+" apply false
}

group "org.mtr.libraries"
version rootProject.properties.version

subprojects {
	apply plugin: "java"
	apply plugin: "maven-publish"
	apply plugin: "com.github.johnrengelman.shadow"

	repositories {
		mavenCentral()
	}

	configurations {
		shadowCompileOnly
		compileOnly.extendsFrom shadowCompileOnly
	}

	java {
		toolchain {
			languageVersion.set(JavaLanguageVersion.of(8))
		}
		withSourcesJar()
		withJavadocJar()
	}

	shadowJar {
		configurations = [project.configurations.shadowCompileOnly]
		archiveClassifier.set("")
		enableRelocation true
		relocationPrefix "org.mtr.libraries"
		exclude "META-INF/**"
	}

	publishing {
		publications {
			maven(MavenPublication) {
				groupId project.group
				artifactId project.name
				version project.version
				from components.java
			}
		}
	}

	tasks.withType(AbstractArchiveTask).configureEach {
		preserveFileTimestamps = false
		reproducibleFileOrder = true
	}
}
